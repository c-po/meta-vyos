DISTRO = "vyos"
DISTRO_NAME = "VyOS - Network Operating System"
DISTRO_VERSION = "0.0.1"
DISTRO_CODENAME = "none"

# This layer contains kernel patches for a variety of machine-specific
# kernels using .bbappend files in recipes-kernel/linux. To allow image
# building without all machine layers being present, we turn 'dangling
# .bbappend file' errors into warnings
BB_DANGLINGAPPENDS_WARNONLY = "1"

# we install dpkg anyways, might as well use .deb packages instead of the
# default .ipk
PACKAGE_CLASSES = "package_deb"

# VyOS configuration requires fuse filesystem to work, so autoload module on
# system bootup
KERNEL_MODULE_AUTOLOAD += " fuse"

INITRAMFS_MAXSIZE ?= "262144"

# Disable unnecessary DISTRO_FEATURES and MACHINE_FEATURES here
DISTRO_FEATURES_remove = " \
    alsa \
    bluetooth \
    cramfs \
    directfb \
    irda \
    nfs \
    opengl \
    pcmcia \
    smbfs \
    usbgadget \
    wayland \
    x11 \
    nfc \
    3g \
    "

MACHINE_FEATURES_remove = " \
    alsa \
    bluetooth \
    irda \
    pcmcia \
    phone \
    qvga \
    touchscreen \
    usbgadget \
    "

DISTRO_FEATURES_append = " \
    pam \
    ldap \
    vyos-core \
    vyos-config \
    vyos-net \
    vyos-wlan \
    vyos-routing \
    vyos-wwan \
    vyos-tunnel \
    vyos-vpn \
    vyos-qos \
    vyos-pppoe \
    vyos-dhcp \
    vyos-monitor \
    vyos-firewall \
    vyos-redundancy \
    vyos-proxy \
    "

DISTRO_FEATURES_append_x86 = " vyos-non-embedded"
DISTRO_FEATURES_append_x86-64 = " vyos-non-embedded"

DISTRO_FEATURES_append_arm = " \
    vyos-embedded \
    vyos-swupdate \
    "

# Image level user/group configuration.
# Inherit extrausers to make the setting of EXTRA_USERS_PARAMS effective.
INHERIT += "extrausers"

EXTRA_USERS_PARAMS = "\
    groupadd -r vyattacfg; \
    groupadd -r vyattaop; \
    groupadd -r quaggavty; \
    groupadd -r fuse; \
    useradd \
        --home /home/vyos \
        --system \
        --gid users \
        --shell /bin/vbash \
        --groups adm,sudo,dip,disk,vyattacfg,quaggavty,fuse \
        --password '' \
        vyos; \
    usermod -P vyos vyos; \
    useradd \
        --home /var/run/quagga \
        --system \
        --user-group \
        --shell /bin/false \
        --comment Vyatta_Quagga_routing_suite \
        --password '' \
        quagga; \
    "

#
# Generic x86 machines should use linux-vyos as Kernel provide
#
PREFERRED_PROVIDER_virtual/kernel_genericx86 = "linux-vyos"
PREFERRED_PROVIDER_virtual/kernel_genericx86-64 = "linux-vyos"
PREFERRED_VERSION_linux-vyos_genericx86 = "4.4%"
PREFERRED_VERSION_linux-vyos_genericx86-64 = "4.4%"

#
# Emulated Images should fallback to linux-yocto
#
PREFERRED_PROVIDER_virtual/kernel_qemux86 = "linux-yocto"
PREFERRED_PROVIDER_virtual/kernel_qemux86-64 = "linux-yocto"
PREFERRED_VERSION_linux-yocto_qemux86 = "4.14%"
PREFERRED_VERSION_linux-yocto_qemux86-64 = "4.14%"
